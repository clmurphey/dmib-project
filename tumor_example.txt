turtles-own [
  stem?     ; true for stem cells, false for transitory cells
  age  ; age of cell. changes color with age
  metastatic?  ; false for progeny of stem cell 0, true for progeny of stem cell 1
]

globals [
  cell-count
]

to setup
  clear-all
  set-default-shape turtles "circle 3"
  ask patches [ set pcolor gray ]
  set-stem
  evaluate-params
  reset-ticks
end

to set-stem   ; create two stem cells
  create-turtles 2 [
    set size 2   ; easier to see
    setxy (min-pxcor / 2) 0
    set stem? true
    set metastatic? false
    set color blue
    set age 0
  ]
  ask turtle 1 [
    set metastatic? true
    set heading 90            ; stem cell 1 will move away
  ]
  set cell-count 2
end

to go
  ask turtles [
    ifelse leave-trail?
      [ pen-down ]
      [ pen-up ]
    if (who = 1) and (xcor < 25) [ fd 1 ]  ; stem cell movement
    set age age + 1
    move-transitional-cells
    mitosis
    death
  ]
  tick
  evaluate-params
end

; transitional cells move and hatch more. Turtle proc.
to move-transitional-cells
  if (not stem?) [
    set color ( red + 0.25 * age )
    fd 1
    if age < 6 [
      hatch 1 [
        ; amplification
        rt random-float 360
        fd 1
      ]
    ]
  ]
end

to mitosis ; turtle proc. - stem cells only
  if stem? [
    hatch 1 [
      fd 1
      set color red
      set stem? false
      ifelse (who = 1)
        [ set age 16 ]
        [ set age 0 ]
    ]
  ]
end

to death ; turtle proc.
  if (not stem?) and (not metastatic?) and (age > 20)
    [ die ]
  if (not stem?) and metastatic? and (age > 4)
    [ die ]
end

to evaluate-params
  set cell-count count turtles  ; cell count
  if (cell-count <= 0) [ stop ]
end

to kill-original-stem-cell
  if turtle 0 != nobody [ ask turtle 0 [ die ] ]
end

to kill-moving-stem-cell
  if turtle 1 != nobody [ ask turtle 1 [ die ] ]
end

to kill-transitory-cells
  ask turtles with [ age < 10 and not stem? ] [ die ]
end


; Copyright 1998 Uri Wilensky.
; See Info tab for full copyright and license.